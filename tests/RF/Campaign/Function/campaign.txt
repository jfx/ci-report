*** Settings ***
Library           Selenium2Library
Resource          ../Conf/campaign.txt
Resource          ../../Common/Function/common.txt
Resource          ../../Common/Function/check.txt
Resource          ../../Project_Dashboard/Function/dashboard.txt
Resource          ../../Suite/Function/suite.txt

*** Keywords ***
Get campaign page location
    [Arguments]    &{campaign}
    ${location} =    Set Variable    ${URL}${location_dashboard}/${campaign.prefid}${location_campaign}/${campaign.crefid}
    [Return]    ${location}

I go to campaign page
    [Arguments]    &{campaign}
    ${location} =    Get campaign page location    &{campaign}
    Go to    ${location}

I should be on campaign page
    [Arguments]    &{campaign}
    ${location} =    Get campaign page location    &{campaign}
    Location Should Be    ${location}

I check campaign displayed values
    [Arguments]    &{campaign}
    Element Should Contain    p-id    \#${campaign.crefid}
    ${enabled} =    Evaluate    ${campaign.passed}+${campaign.failed}+${campaign.errored}+${campaign.skipped}
    Element Should Contain    p-passed    ${campaign.passed} / ${enabled}
    Element Should Contain    p-failed    ${campaign.failed}
    Element Should Contain    p-errored    ${campaign.errored}
    Element Should Contain    p-skipped    ${campaign.skipped}
    Element Should Contain    p-disabled    ${campaign.disabled}
    Element Should Contain    p-start    ${campaign.start}
    Element Should Contain    p-end    ${campaign.end}

The table row should contain values of suite
    [Arguments]    ${row_table}    &{suite}
    ${row} =    Evaluate    ${row_table} + 1
    Table Cell Should Contain    t-suite    ${row}    1    \#${suite.srefid}
    Table Cell Should Contain    t-suite    ${row}    2    ${suite.name}
    ${enabled} =    Evaluate    ${suite.passed}+${suite.failed}+${suite.errored}+${suite.skipped}
    ${percent} =    Get Table Cell    t-suite    ${row}    3
    ${percent_expect} =    Evaluate    ${suite.passed}/${enabled}*100
    ${percent_expect_rounded} =    Convert To Integer    ${percent_expect}
    Should Be Equal    ${percent}    ${percent_expect_rounded} %
    Table Cell Should Contain    t-suite    ${row}    4    ${suite.passed} / ${enabled}
    Table Cell Should Contain    t-suite    ${row}    5    ${suite.failed}
    Table Cell Should Contain    t-suite    ${row}    6    ${suite.errored}
    Table Cell Should Contain    t-suite    ${row}    7    ${suite.skipped}
    Table Cell Should Contain    t-suite    ${row}    8    ${suite.disabled}
    Table Cell Should Contain    t-suite    ${row}    9    ${suite.duration}
    Table Cell Should Contain    t-suite    ${row}    10    ${suite.time}
