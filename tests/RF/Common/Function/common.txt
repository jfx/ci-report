*** Settings ***
Library           OperatingSystem
Library           Process
Library           String
Library           DateTime
Library           Selenium2Library
Library           DatabaseLibrary
Library           RequestsLibrary
Library           ImapLibrary
Resource          ../Conf/common.txt
Resource          ../Conf/mail.txt

*** Keywords ***
I open browser
    Run Keyword If    ${GRID}    Open Browser    ${WEB_URL}    ${BROWSER}    remote_url=${GRID_URL}    desired_capabilities=platform:${DC_OS},version:${DC_VERSION},name:${BUILD_ID} - ${SUITE NAME}/${TEST NAME}
    Run Keyword If    ${HEADLESS}    I open chrome headless browser
    Run Keyword Unless    ${GRID} or ${HEADLESS}    Open Browser    ${WEB_URL}    ${BROWSER}
    Maximize Browser Window
    Run Keyword If    '${ENV}' == 'DEV'    Wait Until Page Contains Element    xpath=//a[@title='Close Toolbar']
    Run Keyword If    '${ENV}' == 'DEV'    Click element    xpath=//a[@title='Close Toolbar']

I open chrome headless browser
    ${options} =    Evaluate    sys.modules['selenium.webdriver'].ChromeOptions()    sys, selenium.webdriver
    Call Method    ${options}    add_argument    headless
    Call Method    ${options}    add_argument    disable-gpu
    Create Webdriver    Chrome    chrome_options=${options}
    Goto    ${WEB_URL}

I create an API session named "cir"
    Create Session    cir    ${API_URL}    disable_warnings=1

I create a connexion to database
    ${matches} =    Get Regexp Matches    ${DB_URL}    :\/\/(.*):(.*)@(.*):(.*)/(.*)    1    2    3
    ...    4    5
    ${db_url} =    Get From List    ${matches}    0
    ${username} =    Get From List    ${db_url}    0
    ${password} =    Get From List    ${db_url}    1
    ${host} =    Get From List    ${db_url}    2
    ${port} =    Get From List    ${db_url}    3
    ${database} =    Get From List    ${db_url}    4
    Connect To Database    dbapiModuleName=MySQLdb    dbHost=${host}    dbPort=${port}    dbName=${database}    dbUsername=${username}    dbPassword=${password}

Load DB
    Run Keyword If    '${DB_DUMP_FILE}' == '${EMPTY}'    Load fixtures DB
    Run Keyword Unless    '${DB_DUMP_FILE}' == '${EMPTY}'    Load dump DB

Load fixtures DB
    Log    -- Load fixtures DB --
    ${rc} =    Run And Return Rc    ${BIN_DIR}/loadDBFixtures.sh
    Should Be Equal As Integers    ${rc}    0

Load dump DB
    Log    -- Load dump DB --
    ${rc} =    Run And Return Rc    ${BIN_DIR}/dumpDB.sh -i ${DB_URL} \ ${DB_DUMP_FILE}
    Should Be Equal As Integers    ${rc}    0

Setup
    Log    -- Setup --
    : FOR    ${tag}    IN    @{TEST TAGS}
    \    Run Keyword If    '${tag}' == 'DB'    I create a connexion to database
    : FOR    ${tag}    IN    @{TEST TAGS}
    \    Run Keyword If    '${tag}' == 'GUI'    I open browser
    : FOR    ${tag}    IN    @{TEST TAGS}
    \    Run Keyword If    '${tag}' == 'API'    I create an API session named "cir"
    : FOR    ${tag}    IN    @{TEST TAGS}
    \    Run Keyword If    '${tag}' == 'MAIL'    Open Mailbox    host=${IMAP_SERVER}    user=${MAIL_USER}    password=${MAIL_PASSWORD}

Teardown
    Log    -- Teardown --
    : FOR    ${tag}    IN    @{TEST TAGS}
    \    Run Keyword If    '${tag}' == 'MAIL'    Close Mailbox
    : FOR    ${tag}    IN    @{TEST TAGS}
    \    Run Keyword If    '${tag}' == 'API'    Delete All Sessions
    : FOR    ${tag}    IN    @{TEST TAGS}
    \    Run Keyword If    '${tag}' == 'GUI'    Close All Browsers
    : FOR    ${tag}    IN    @{TEST TAGS}
    \    Run Keyword If    '${tag}' == 'EDIT'    Load DB
    : FOR    ${tag}    IN    @{TEST TAGS}
    \    Run Keyword If    '${tag}' == 'DB'    Disconnect from Database

the breadcrumb should contain
    [Arguments]    @{list}
    ${i} =    Set variable    0
    : FOR    ${item}    IN    @{list}
    \    ${i} =    Evaluate    ${i} + 1
    \    ${level} =    Get Text    br_lvl${i}
    \    Should Be Equal As Strings    ${item}    ${level}

I click on breadcrumb level
    [Arguments]    ${level}
    Click Link    id=br_lvl${level}

I get content of last email
    [Arguments]    ${subject}
    ${latest}=    Wait for Mail    sender=${MAIL_SENDER}    recipient=${MAIL_USER}    subject=${subject}    status=UNSEEN    timeout=300
    ${content}=    Get Email body    ${latest}
    [Return]    ${content}

I get href from html link id
    [Arguments]    ${content}    ${id}
    ${line}=    Get Lines Containing String    ${content}    ${id}
    ${link} =    Get Regexp Matches    ${line}    href="(.*?)"    1
    [Return]    ${link[0]}

I get list from js array
    [Arguments]    ${content}    ${id}
    ${lines}=    Get Lines Containing String    ${content}    ${id}
    ${line}=    Get Lines Containing String    ${lines}    var
    ${array} =    Get Regexp Matches    ${line}    \\[(.*?),\\]    1
    @{list} =    Split String    ${array[0]}    ,
    [Return]    @{list}
