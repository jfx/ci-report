*** Settings ***
Library           Selenium2Library
Resource          ../Conf/suite.txt
Resource          ../../Common/Function/common.txt
Resource          ../../Common/Function/check.txt
Resource          ../../Project_Dashboard/Function/dashboard.txt
Resource          ../../Campaign/Function/campaign.txt

*** Keywords ***
Get suite page location
    [Arguments]    &{suite}
    ${location} =    Set Variable    ${URL}${location_dashboard}/${suite.prefid}${location_campaign}/${suite.crefid}${location_suite}/${suite.srefid}
    [Return]    ${location}

I go to suite page
    [Arguments]    &{suite}
    ${location} =    Get suite page location    &{suite}
    Go to    ${location}

I should be on suite page
    [Arguments]    &{suite}
    ${location} =    Get suite page location    &{suite}
    Location Should Be    ${location}

I check suite displayed values
    [Arguments]    &{suite}
    Element Should Contain    p-id    ${suite.name}
    ${enabled} =    Evaluate    ${suite.passed}+${suite.failed}+${suite.errored}+${suite.skipped}
    Element Should Contain    p-passed    ${suite.passed} / ${enabled}
    Element Should Contain    p-failed    ${suite.failed}
    Element Should Contain    p-errored    ${suite.errored}
    Element Should Contain    p-skipped    ${suite.skipped}
    Element Should Contain    p-disabled    ${suite.disabled}
    Element Should Contain    p-duration    ${suite.duration}
    Element Should Contain    p-time    ${suite.time}
