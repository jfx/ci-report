*** Settings ***
Library           Selenium2Library
Resource          ../Conf/suite.txt
Resource          ../../Project_Dashboard/Conf/dashboard.txt
Resource          ../../Campaign/Conf/campaign.txt

*** Keywords ***
Get suite page location
    [Arguments]    &{suite}
    ${location} =    Set Variable    ${URL}${location_dashboard}/${suite.pid}${location_campaign}/${suite.cid}${location_suite}/${suite.id}
    [Return]    ${location}

I go to suite page
    [Arguments]    &{suite}
    ${location} =    Get suite page location    &{suite}
    Go to    ${location}

I should be on suite page
    [Arguments]    &{suite}
    ${location} =    Get suite page location    &{suite}
    Location Should Be    ${location}

I check suite displayed values
    [Arguments]    &{suite}
    Element Should Contain    p-id    \#${suite.id}
    ${enabled} =    Evaluate    ${suite.passed}+${suite.failed}+${suite.errored}+${suite.skipped}
    Element Should Contain    p-passed    ${suite.passed} / ${enabled}
    Element Should Contain    p-failed    ${suite.failed}
    Element Should Contain    p-errored    ${suite.errored}
    Element Should Contain    p-skipped    ${suite.skipped}
    Element Should Contain    p-disabled    ${suite.disabled}
    Element Should Contain    p-duration    ${suite.duration}
    Element Should Contain    p-time    ${suite.time}
